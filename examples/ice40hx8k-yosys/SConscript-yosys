Import('env')

env.SetDefault(YOSYS = 'yosys')
env.SetDefault(YOSYS_CMD = '')
env.SetDefault(SYNTHCOM = 'yosys -p "$YOSYS_CMD" $SOURCES -q')
env.SetDefault(SYNTHCOMSTR = '$SYNTH')

env.SetDefault(ARACHNE = 'arachne-pnr')
# See http://www.clifford.at/icestorm/ for target specifications
env.SetDefault(PLACEROUTEOPTS = '')
env.SetDefault(PLACEROUTECOM = '$ARACHNE -q $PLACEROUTEOPTS $SOURCE -o $TARGET')
env.SetDefault(PLACEROUTECOMSTR = '$PLACEROUTE')

env.SetDefault(ICEPACK = 'icepack')
env.SetDefault(BITGENCOM = '$ICEPACK $SOURCE $TARGET > /dev/null')
env.SetDefault(BITGENCOMSTR = '$BITGEN')

env.SetDefault(ICEPROG = 'iceprog')
env.SetDefault(BITPROGOPTS = '')
env.SetDefault(BITPROGCOM = '$ICEPROG $BITPROGOPTS $SOURCE 2> /dev/null')
env.SetDefault(BITPROGCOMSTR = '$BITPROG')

env.SetDefault(ICETIME = 'icetime')
env.SetDefault(TIMINGOPTS = '')
env.SetDefault(TIMINGCOM = '$ICETIME $TIMINGOPTS $SOURCE > $TARGET')
env.SetDefault(TIMINGCOMSTR = '$TIMINGCOM')

env.Append(BUILDERS = {'Synthesize': Builder(
  action=Action('$SYNTHCOM', '$SYNTHCOMSTR'),
  src_suffix='.v',
  suffix='.blif'
)})

env.Append(BUILDERS = {'_PlaceRoute': Builder(
  action=Action('$PLACEROUTECOM', '$PLACEROUTECOMSTR'),
  src_suffix='.blif',
  suffix='.asc'
)})

def PlaceRoute(env, *args, **kwargs):
  if 'pcf' in kwargs:
    pcf = kwargs['pcf']
    del kwargs['pcf']
  else:
    pcf = None

  if pcf is not None:
    env = env.Clone(
      PCFFILE=pcf,
      PLACEROUTEOPTS=env['PLACEROUTEOPTS'] + ' -p $PCFFILE'
    )
  target = env._PlaceRoute(*args)
  if pcf is not None:
    Depends(target, pcf)
  return target

env.AddMethod(PlaceRoute)

env.Append(BUILDERS = {'BitGen': Builder(
  action=Action('$BITGENCOM', '$BITGENCOMSTR'),
  src_suffix='.asc',
  suffix='.bin'
)})

env.Append(BUILDERS = {'BitProg': Builder(
  action=Action('$BITPROGCOM', '$BITPROGCOMSTR'),
  src_suffix='.bin'
)})

env.Append(BUILDERS = {'Timing': Builder(
  action=Action('$TIMINGCOM', '$TIMINGCOMSTR'),
  suffix='.rpt',
  src_suffix='.asc'
)})
